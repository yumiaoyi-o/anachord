import ".."
import qs.services
import qs.config
import QtQuick

StyledRect {
    id: root

    enum Type {
        Filled,
        Tonal,
        Text
    }

    property alias icon: label.text
    property bool checked
    property bool toggle
    property real padding: type === IconButton.Text ? Appearance.padding.small / 2 : Appearance.padding.smaller
    property alias font: label.font
    property int type: IconButton.Filled
    property bool disabled

    property alias stateLayer: stateLayer
    property alias label: label
    property alias radiusAnim: radiusAnim

    property bool internalChecked
    property color activeColour: type === IconButton.Filled ? ComponentColors.region.panel.accent : ComponentColors.region.panel.accent
    property color inactiveColour: {
        if (!toggle && type === IconButton.Filled)
            return ComponentColors.region.panel.accent;
        return type === IconButton.Filled ? Qt.alpha(ComponentColors.region.panel.surface, Colours.tPalette.m3surfaceContainer.a) : ComponentColors.region.panel.button.tonalBg;
    }
    property color activeOnColour: type === IconButton.Filled ? ComponentColors.region.panel.onAccent : type === IconButton.Tonal ? ComponentColors.region.panel.onAccent : ComponentColors.region.panel.accent
    property color inactiveOnColour: {
        if (!toggle && type === IconButton.Filled)
            return ComponentColors.region.panel.onAccent;
        return type === IconButton.Tonal ? ComponentColors.region.panel.button.tonalText : ComponentColors.region.panel.subtext;
    }
    property color disabledColour: Qt.alpha(ComponentColors.region.panel.text, 0.1)
    property color disabledOnColour: Qt.alpha(ComponentColors.region.panel.text, 0.38)

    signal clicked

    onCheckedChanged: internalChecked = checked

    radius: internalChecked ? Appearance.rounding.small : implicitHeight / 2 * Math.min(1, Appearance.rounding.scale)
    color: type === IconButton.Text ? "transparent" : disabled ? disabledColour : internalChecked ? activeColour : inactiveColour

    implicitWidth: implicitHeight
    implicitHeight: label.implicitHeight + padding * 2

    StateLayer {
        id: stateLayer

        color: root.internalChecked ? root.activeOnColour : root.inactiveOnColour
        disabled: root.disabled

        function onClicked(): void {
            if (root.toggle)
                root.internalChecked = !root.internalChecked;
            root.clicked();
        }
    }

    MaterialIcon {
        id: label

        anchors.centerIn: parent
        color: root.disabled ? root.disabledOnColour : root.internalChecked ? root.activeOnColour : root.inactiveOnColour
        fill: !root.toggle || root.internalChecked ? 1 : 0

        Behavior on fill {
            Anim {}
        }
    }

    Behavior on radius {
        Anim {
            id: radiusAnim
        }
    }
}
